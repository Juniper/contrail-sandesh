# -*- mode: python; -*-

#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

Import('SandeshEnv')
import sys

env = SandeshEnv.Clone();

env.Append(CPPPATH = [
        Dir('#build/include').abspath,
        Dir('#controller/src').abspath,
        Dir('#tools').abspath,
        '../../common/test/',
        './gen-c/',
        ])

# Generate the source files
SandeshCTestGenFiles = env.SandeshGenC('sandesh_c_test.sandesh')
SandeshCTestGenSrcs = env.ExtractC(SandeshCTestGenFiles)

SandeshLibPath = ['#/build/lib',
                  Dir(env['TOP']).abspath + '/base',
                  Dir(env['TOP']).abspath + '/base/test',
                  Dir(env['TOP']).abspath + '/io',
                  ]

SandeshLibs = ['gunit',
               'sandesh',
               'sandesh-c',
               'http',
               'http_parser',
               'curl',
               'connection_info',
               'io',
               'sandeshvns',
               'base',
               'boost_system',
               'boost_regex',
               'boost_date_time',
               'log4cplus',
               'tbb_debug',
               'pthread',
               'curl',
               'xml2',
               'xslt',
               'task_test',
               'pugixml',
               ]

env.Append(LIBPATH = SandeshLibPath)

env.Prepend(LIBS = SandeshLibs)

if sys.platform != 'darwin':
    env.Append(LIBS = ['rt'])

sandesh_pre_c_test = env.UnitTest('sandesh_pre_c_test',
                                  env['SandeshBufferTestGenCppObjs'] +
                                  ['sandesh_pre_c_test.cc']
                                  )

env.Alias('src/sandesh:sandesh_pre_c_test', sandesh_pre_c_test)

sandesh_c_test = env.UnitTest('sandesh_c_test',  
                              SandeshCTestGenSrcs +
                              env['SandeshBufferTestGenCObjs'] +  
                              ['sandesh_c_test.cc']
                              )

env.Alias('src/sandesh:sandesh_c_test', sandesh_c_test)
env.Depends(sandesh_c_test, env.Alias('src/sandesh:sandesh_pre_c_test'))

test_suite = [sandesh_pre_c_test,
              sandesh_c_test]

test = env.TestSuite('sandesh-test', test_suite)
env.Alias('src/sandesh:ctest', test)
Return('test_suite')
