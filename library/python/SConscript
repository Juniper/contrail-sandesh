# -*- mode: python; -*-

#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

Import('SandeshEnv');

env = SandeshEnv.Clone();

LOCAL_PATH = "#/src/sandesh/library/python/"
PYSANDESH_PATH = LOCAL_PATH + 'pysandesh/'
PYSANDESH_INSTALL_PATH = 'pysandesh/'

local_modules = [ 
                 'setup.py',
                 'MANIFEST.in'
                ]

local_modules_rules = []
for module in local_modules:
    local_modules_rules.append(env.Install('', LOCAL_PATH + module))

web_srv_rules = []
web_srv_files = [
('#/src/sandesh/library/common/webs','css/bootstrap.min.css'),
('#/src/sandesh/library/common/webs','css/DT_bootstrap.css'),
('#/src/sandesh/library/common/webs','css/images/sort_asc.png'),
('#/src/sandesh/library/common/webs','css/images/sort_asc_disabled.png'),
('#/src/sandesh/library/common/webs','css/images/sort_both.png'),
('#/src/sandesh/library/common/webs','css/images/sort_desc.png'),
('#/src/sandesh/library/common/webs','css/images/sort_desc_disabled.png'),
('#/src/sandesh/library/common/webs','css/style.css'),
('#/src/sandesh/library/common/webs','js/bootstrap.min.js'),
('#/src/sandesh/library/common/webs','js/DT_bootstrap.js'),
('#/src/sandesh/library/common/webs','js/jquery-2.0.3.min.js'),
('#/src/sandesh/library/common/webs','js/jquery.dataTables.min.js'),
('#/src/sandesh/library/common/webs','js/util.js'),
('#/src/sandesh/library/common/webs','universal_parse.xsl')]

for elem in web_srv_files:
    terms = elem[1].rsplit("/",1)
    if len(terms) == 1:
        hpath = ""
        hfile = terms[0]
    else:
        hpath = "/" + terms[0]
        hfile = terms[1]

    web_srv_rules.append(env.Install(PYSANDESH_INSTALL_PATH + '/webs' + hpath, elem[0] + "/" + elem[1]))

pysandesh_modules = [
                     '__init__.py',
                     'Thrift.py',
                     'tcp_session.py',
                     'work_queue.py',
                     'util.py',
                     'trace.py',
                     'sandesh_base.py',
                     'sandesh_client.py',
                     'sandesh_connection.py',
                     'sandesh_http.py',
                     'sandesh_logger.py',
                     'sandesh_session.py',
                     'sandesh_state_machine.py',
                     'sandesh_uve.py',
                     'sandesh_req_impl.py',
                     'sandesh_stats.py',
                     'sandesh_trace.py'
                    ]

pysandesh_modules_rules = []
for module in pysandesh_modules:
    pysandesh_modules_rules.append(env.Install(PYSANDESH_INSTALL_PATH, PYSANDESH_PATH + module))

fysom_sources_rules = []
fysom_sources_rules.append(env.Install(PYSANDESH_INSTALL_PATH, "#/third_party/fysom/fysom.py"))       

bottle_sources_rules = []
bottle_sources_rules.append(env.Install(PYSANDESH_INSTALL_PATH, '#/third_party/bottle-0.11.6/bottle.py'))    

transport_modules = [
                     '__init__.py',
                     'TTransport.py'
                    ]

transport_modules_rules = []
for module in transport_modules:
    transport_modules_rules.append(env.Install(PYSANDESH_INSTALL_PATH + 'transport/', PYSANDESH_PATH + 'transport/' + module))

protocol_modules = [
                    '__init__.py',
                    'TProtocol.py',
                    'TBinaryProtocol.py',
                    'TXMLProtocol.py'
                   ]

protocol_modules_rules = []
for module in protocol_modules:
    protocol_modules_rules.append(env.Install(PYSANDESH_INSTALL_PATH + 'protocol/', PYSANDESH_PATH + 'protocol/' + module))

# Auto-generated SandeshHeader package 
sandesh_hdr_pkg = env.SandeshGenPy('#src/sandesh/library/common/sandesh.sandesh', 'pysandesh/')
# Auto-generated Sandesh Ctrl package
sandesh_ctrl_pkg = env.SandeshGenPy('#src/sandesh/library/common/sandesh_ctrl.sandesh', 'pysandesh/') 
# Auto-generated Sandesh UVE package 
sandesh_uve_pkg = env.SandeshGenPy('#src/sandesh/library/common/sandesh_uve.sandesh', 'pysandesh/')
# Auto-generated Sandesh Req package
sandesh_req_pkg = env.SandeshGenPy('#src/sandesh/library/common/sandesh_req.sandesh', 'pysandesh/')
# Auto-generated Sandesh Trace package
sandesh_trace_pkg = env.SandeshGenPy('#src/sandesh/library/common/sandesh_trace.sandesh', 'pysandesh/')

sdist_depends = [local_modules_rules, web_srv_rules,
                 pysandesh_modules_rules, transport_modules_rules, 
                 protocol_modules_rules, sandesh_hdr_pkg,
                 sandesh_ctrl_pkg, sandesh_uve_pkg, sandesh_req_pkg,
                 sandesh_trace_pkg,
                 fysom_sources_rules, bottle_sources_rules]

env['pysandesh_target'] = sdist_depends

sdist_gen = env.Command('dist', 'setup.py', 'python setup.py sdist', chdir=1)

# install everything before building distribution
env.Depends(sdist_gen, sdist_depends)

env.Default(sdist_gen)

SConscript('pysandesh/SConscript', exports='env', duplicate = 0)
